from std.memory import UnsafePointer, memcpy
from std.collections import InlineArray

struct CpuMask[size: Int = 128](Copyable):
    var bytes: InlineArray[UInt8, Self.size]

    def __init__(out self):
        self.bytes = InlineArray[UInt8, Self.size](fill=0)

    def set(mut self, cpu_id: Int):
        var byte_idx = cpu_id >> 3
        var bit_idx = cpu_id & 7
        if byte_idx < Self.size:
            self.bytes[byte_idx] |= UInt8(1 << bit_idx)

    def clear(mut self, cpu_id: Int):
        var byte_idx = cpu_id >> 3
        var bit_idx = cpu_id & 7
        if byte_idx < Self.size:
            self.bytes[byte_idx] &= ~UInt8(1 << bit_idx)

    def test(ref self, cpu_id: Int) -> Bool:
        var byte_idx = cpu_id >> 3
        var bit_idx = cpu_id & 7
        if byte_idx >= Self.size:
            return False
        return (self.bytes[byte_idx] & UInt8(1 << bit_idx)) != 0

    def clear_all(mut self):
        for i in range(Self.size):
            self.bytes[i] = 0

    def set_all(mut self):
        for i in range(Self.size):
            self.bytes[i] = 0xFF

    def count(ref self) -> Int:
        var total = 0
        for i in range(Self.size):
            var b = self.bytes[i]
            while b != 0:
                total += Int(b & 1)
                b >>= 1
        return total

    def ptr(ref self) -> UnsafePointer[UInt8, MutAnyOrigin]:
        return UnsafePointer[UInt8, MutAnyOrigin](unsafe_from_address=Int(UnsafePointer(to=self.bytes)))

    def copy_to(ref self, dest: UnsafePointer[UInt8, MutAnyOrigin]):
        memcpy(dest=dest, src=self.ptr(), count=Self.size)

    @staticmethod
    def from_cpu_list(cpu_ids: List[Int]) -> Self:
        var mask = Self()
        for i in range(len(cpu_ids)):
            mask.set(cpu_ids[i])
        return mask^

    @staticmethod
    def byte_size() -> Int:
        return Self.size
